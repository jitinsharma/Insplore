apply plugin: 'com.android.application'

import groovy.json.JsonSlurper
import groovy.xml.MarkupBuilder

android {
    signingConfigs {
        release {
            keyAlias 'insplore'
            keyPassword 'insplore'
            storeFile file('keystore/key.jks')
            storePassword 'insplore'
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "io.github.jitinsharma.insplore"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    //compile 'com.google.android.gms:play-services:9.0.2'
    compile 'com.google.android.gms:play-services-places:9.2.0'
    compile 'com.google.android.gms:play-services-maps:9.2.0'
    compile 'com.google.android.gms:play-services-location:9.2.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:palette-v7:23.4.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:customtabs:23.4.0'
    compile 'com.android.volley:volley:1.0.0'
}

task getCitiesAsString {
    doLast {
        String path = "app/src/main/assets/yapq_cities.json"
        def inputFile = new File(path)
        def json = new JsonSlurper().parseText(inputFile.text)
        def cities = json
        def sw = new StringWriter()
        def xml = new MarkupBuilder(sw)
        xml.resources {
            'string-array'(name: "yapq_cities") {
                for (int i = 0; i < cities.size; i++) {
                    def value = cities.get(i).city.toString()
                    println value
                    item(value)
                }
            }
        }
        def stringsFile = new File("app/src/main/res/values/cities.xml")
        stringsFile.write(sw.toString())
    }
}

task getAirportsAsString{
    doLast {
        String path = "app/src/main/assets/airports.json"
        def inputFile = new File(path)
        def json = new JsonSlurper().parseText(inputFile.text)
        def airports = json
        def sw = new StringWriter()
        def xml = new MarkupBuilder(sw)
        xml.resources {
            'string-array'(name: "airport_cities") {
                for (int i = 0; i < airports.size; i++) {
                    def value = airports.get(i).code.toString() + ", " + airports.get(i).city.toString()
                    //println value
                    item("\""+value+"\"")
                }
            }
        }
        def stringsFile = new File("app/src/main/res/values/airports.xml")
        stringsFile.write(sw.toString())
    }
}
